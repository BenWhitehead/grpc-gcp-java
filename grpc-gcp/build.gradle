plugins {
    id 'java'
    id 'maven'
    id 'com.google.protobuf' version '0.8.10'
    id 'com.github.sherter.google-java-format' version '0.8'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenLocal()
}


def grpcVersion = '1.21.1' 
def protobufVersion = '3.8.0'
def protocVersion = '3.5.1-1'

dependencies {
    compile 'commons-io:commons-io:2.5'
    compile 'com.google.api:api-common:1.2.0'
    compile 'com.google.cloud:google-cloud-logging:1.53.0'
    compile 'com.google.auth:google-auth-library-oauth2-http:0.9.0'
    compile 'com.google.auth:google-auth-library-parent:0.9.0'
    compile "com.google.protobuf:protobuf-java:${protobufVersion}"
    compile "io.grpc:grpc-netty-shaded:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-auth:${grpcVersion}"
    compile 'com.google.api.grpc:grpc-google-cloud-bigtable-v2:0.39.0'
    compile 'com.google.cloud:google-cloud-spanner:0.71.0-beta'
    testCompile "io.grpc:grpc-testing:${grpcVersion}"
    testCompile 'com.google.truth:truth:0.42'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'junit', name: 'junit-dep', version: '4.10'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.+'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
    benchmarkTest {
        java.srcDir file('src/benchmarkTest/java')
        resources.srcDir file('src/benchmarkTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task bigtableLoadTest(type: JavaExec) {
    classpath = sourceSets.benchmarkTest.runtimeClasspath
    main = 'BigtableLoadTest'
}

task spannerLoadTest(type: JavaExec) {
    classpath = sourceSets.benchmarkTest.runtimeClasspath
    main = 'SpannerLoadTest'
}

task spannerBenchmark(type: JavaExec) {
    classpath = sourceSets.benchmarkTest.runtimeClasspath
    main = 'SpannerBenchmark'
}

group = 'com.google.cloud'
version = '0.0.1-SNAPSHOT'
description = 'GRPC-GCP-Extension Java'
sourceCompatibility = '1.8'
